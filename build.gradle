apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'nexus'
apply plugin: 'org.standardout.versioneye'

version = '2.2.2'

sourceCompatibility = 1.7
targetCompatibility = 1.7

compileJava.options.encoding = 'UTF-8'

ext {
    title = 'log4j-systemd-journal-appender'
    description = 'Log4j appender for systemd journal.'
    url = 'https://www.github.com/bwaldvogel/log4j-systemd-journal-appender'
}

jar {
    manifest {
        attributes 'Implementation-Title': title, 'Implementation-Version': version
    }
}

// https://github.com/bmuschko/gradle-nexus-plugin
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
        classpath 'org.standardout:gradle-versioneye-plugin:1.2.0'
    }
}

ext {
    log4jVersion = '2.10.0'
    powermockVersion = '1.6.6'
}

modifyPom {
    project {
        groupId        'de.bwaldvogel'
        artifactId     'log4j-systemd-journal-appender'
        name           title
        description    description
        url            url
        inceptionYear '2014'

        scm {
            url 'git@github.com:bwaldvogel/log4j-systemd-journal-appender.git'
            connection 'scm:git:git@github.com:bwaldvogel/log4j-systemd-journal-appender.git'
            developerConnection 'scm:git:git@github.com:bwaldvogel/log4j-systemd-journal-appender.git'
        }

        licenses {
            license {
                name 'The BSD License'
                url 'http://www.opensource.org/licenses/bsd-license.php'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'bwaldvogel'
                name 'Benedikt Waldvogel'
                email 'mail@bwaldvogel.de'
            }
        }
    }
}

nexus {
    attachSources = true
    attachTests = false
    attachJavadoc = true
    sign = true
}

repositories {
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

dependencies {
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4jVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion
    compile group: 'net.java.dev.jna', name: 'jna', version: '4.5.1'

    testCompile group: 'junit', name: 'junit', version: '4.12'

    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: powermockVersion
    testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: powermockVersion
}

task copyDependencies(type:Copy) {
    new File('build/libs').mkdirs()
    copy {
        from configurations.runtime
        into 'build/libs'
    }
}

// vim: set ts=4 sw=4 et:
